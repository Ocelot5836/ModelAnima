plugins {
    id 'forgified-fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "${project.minecraft_version}-${project.mod_version}"
group = 'io.github.ocelot'
archivesBaseName = project.mod_id

loom {
    silentMojangMappingsLicense()

    runs {
        client {
            runDir "run/client"
        }
        server {
            runDir "run/server"
        }
    }

    localMods {
        it.add(sourceSets.test)
    }
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings minecraft.officialMojangMappings()

    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    // These mods make life easier during runtime. They aren't strictly necessary
    modRuntime "curse.maven:configured-457570:3340535"
    modRuntime "curse.maven:catalogue-459701:3335979"

    modImplementation "com.github.Ocelot5836:Sonar:${project.sonar_version}"
}

processResources {
    inputs.property("mod_version", project.mod_version)
    inputs.property("mod_id", project.mod_id)
    filesMatching(["META-INF/mods.toml", "pack.mcmeta"]) {
        expand 'mod_version': project.mod_version, 'mod_id': project.mod_id
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : "ModelAnima",
                "Specification-Vendor"    : "Ocelot",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : "Ocelot",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'FMLModType'              : 'LIBRARY'
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        if (project.hasProperty("jacksonMavenUser") && project.hasProperty("jacksonMavenPass")) {
            maven {
                url "https://maven.jaackson.me"
                credentials {
                    username = project.properties.jacksonMavenUser
                    password = project.properties.jacksonMavenPass
                }
            }
        }
    }
}
